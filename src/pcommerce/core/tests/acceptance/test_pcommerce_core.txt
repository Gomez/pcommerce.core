*** Settings ***

Library  Selenium2Library  run_on_failure=Capture Page Screenshot
Variables  plone/app/testing/interfaces.py

Resource  plone/act/keywords.txt

Suite Setup  Start browser
Suite Teardown  Close All Browsers

*** Variables ***

${front-page}  http://localhost:55001/plone/
${test-folder}  http://localhost:55001/plone/pcommerce-test-folder
${control-panel}  http://localhost:55001/plone/@@overview-controlpanel

${PORT} =  55001
${ZOPE_URL} =  http://localhost:${PORT}
${PLONE_URL} =  ${ZOPE_URL}/plone

*** Keywords ***

Start browser
    Open browser  ${front-page}

Create Product
    [Arguments]  ${title}
    Go to  ${front-page}/createObject?type_name=Product
    Input text  title  ${title}
    Click Button  Save

Go through checkout 
    Go to  ${front-page}/my-product
    Click Button  add to cart
    Go to  ${front-page}/@@cart
    Click Button  Check out
    Input text  firstname  My firstname
    Input text  lastname  My lastname
    Input text  address1  My address1
    Input text  zip  000000
    Input text  city  My city
    Input text  country  My country
    Input text  email  email@email.com
    Input text  phone  000000 0000

*** Test Cases ***

Test Noregistration switch visible  
    Log in as site owner
    Go to  ${front-page}/@@pcommerce_configlet

    Element Should Be Visible  id=noregistration_order

Test Registration needed
    Log in as site owner
    Create Product  My product
    Page should contain  Changes saved.
    #Workflow Publish
    Log out
    Go to  ${front-page}/my-product
    Click Button  add to cart
    Page should contain  Added item to cart
    Go to  ${front-page}/@@cart

    Page should contain  You have to be registered to check out your order,

Test No Registration needed
    Log in as site owner
    Create Product  My product
    Page should contain  Changes saved.
    Go to  ${front-page}/@@pcommerce_configlet
    Click Element  id=noregistration_order 
    Click Button  Save
    Page should contain  Properties saved
    Log out
    Go to  ${front-page}/my-product
    Click Button  add to cart
    Go to  ${front-page}/@@cart

    Page should not contain  You have to be registered to check out your order,

Test Go through checkout as anonymous
    Log in as site owner
    Create Product  My product
    Page should contain  Changes saved.
    Go to  ${front-page}/@@pcommerce_configlet
    Click Element  id=noregistration_order 
    Click Button  Save
    Page should contain  Properties saved
    Log out
    Go through checkout
    Click Button  Next step
    Page should contain  equal to billing address
    Click Button  Next step
    Page should contain  equal to customer address
    Click Button  Next step
    Page should contain  Overview 
    Click Element  id=gtc_box
    Click Button  Send order  
    Page should contain  We successfully received your order.

 
